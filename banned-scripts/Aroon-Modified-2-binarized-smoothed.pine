//@version=3
study("Aroon Modified 2: binarized and smoothed")

// Three views of Aroon Indicator - original, binarized and smoothed.
// More 'readable' variations of this indicator will help to appreciate its role and integrate into trading strategies more readily.
// Note that the smoothed Aroon can be viewed as a channel indicator when normalized to price series.

// UPDATED Smoothed Aroon view!

ifisher(src, len) =>
    hi = highest(src, len)
    lo = lowest(src, len)
    value0 = 0.0, ifisher = 0.0
    value0 := .66 * ((src - lo) / max(hi - lo, .001) - .5) + .67 * nz(value0[1])
    value1 = value0 > .99 ? .999 : value0 < -.99 ? -.999 : value0  //round
    ifisher := .5 * log((1 + value1) / max(1 - value1, .001)) + .5 * nz(ifisher[1])
    ifisher
    
normalize(src, len) =>
    hi  = highest(src, len)
    lo  = lowest(src, len)
    res = (src - lo)/(hi - lo)
    
// Inputs

p    = input(2, title="Lookback Window", minval=1)
type = input("Smoothed Aroon", title="Indicator View", options=["Original Aroon","Binarized Aroon","Smoothed Aroon"])

// Calc

upper = 100 * (highestbars(high, p+1) + p)/p
lower = 100 * (lowestbars(low, p+1) + p)/p
aup   = type=="Original Aroon" ? upper : type=="Binarized Aroon" ? normalize(ifisher(upper, p), 2) : normalize(ifisher(upper, p), 100)
adn   = type=="Original Aroon" ? lower : type=="Binarized Aroon" ? normalize(ifisher(lower, p), 2) : normalize(ifisher(lower, p), 100)

// Plotting

col_up = #0094FF, col_dn = #FF6A00
plot(                         aup,        color=type=="Original Aroon" ? #FF6A00 : col_up, transp=0)
                              //***UPDATE***
plot(type=="Binarized Aroon" ? -adn : adn, color=type=="Original Aroon" ? #0094FF : col_dn, transp=0)
