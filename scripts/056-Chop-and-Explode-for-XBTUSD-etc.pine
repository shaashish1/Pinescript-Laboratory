//@version=4
study("Chop and Explode for XBTUSD, etc", '', false)

// original: fhenry0331
// modified: capissimo

// The purpose of this script is to decipher chop zones from runs/movement/explosion 
// The chop is RSI movement between 40 and 60.
// Tight chop is RSI movement between 45 and 55. 
// There should be an explosion after RSI breaks through 60 (long) or 40 (short). 
// Tight chop bars are colored black, 
// a series of black bars is tight consolidation and should explode imminently. 
// The longer the chop the longer the explosion will go for. tighter the better. 
// Loose chop (whip saw/yellow bars) will range between 40 and 60. 
// The move begins with blue bars for long and purple bars for short. 
// Couple it with your trading system to help stay out of chop and enter when there is movement. 
// Use with "Simple Trender." 

//*** Functions
scaleMinimax(X, p, min, max) => 
    hi = highest(X, p), lo = lowest(X, p)
    (max - min) * (X - lo)/(hi - lo) + min

//*** Inputs
prix  = input(close, "Price Data")
p     = input(14,    "Lookback Window", minval=1)
mmx   = input(14,    "Scaler Lookback", minval=1)

//*** Main
price = scaleMinimax(prix, mmx, 0, 100)  // In general, the best solution lies in scaling the price
up    = rma(max(change(price), 0), p)
down  = rma(-min(change(price), 0), p)
rsi   = down==0 ? 100 : up==0 ? 0 : 100 - (100 / (1 + up / down))

change1 = rsi > 60 ? color.blue :na
change2 = rsi < 40 ? color.purple: na
cond1   = rsi > 60 and price[1] < rsi and price > 0 
cond2   = rsi < 40 and price[1] < rsi and price > 0 
cond3   = rsi > 55 and price[1] < rsi and price < 60 
cond4   = rsi > 45 and price[1] < rsi and price[1] < 55 
cond5   = rsi > 40 and price[1] < rsi and price[1] < 55 

plot(rsi, color=color.black, linewidth=1, transp=0)
plot(rsi, style=plot.style_linebr, linewidth=3, color=change1)
plot(rsi, style=plot.style_linebr, linewidth=3, color=change2)

band1   = hline(70)
band2   = hline(30)
band3   = hline(60)
band4   = hline(40)
midline = hline(50)
band5   = hline(55)
band6   = hline(45)
fill(band1, band3, color=color.green,  transp=90)
fill(band3, band4, color=color.yellow, transp=90)
fill(band4, band2, color=color.red,    transp=90)
fill(band5, band6, color=color.black,  transp=50)

c = cond1 ? color.blue : cond2 ? color.purple : cond3 ? color.yellow : cond4 ? color.black : cond5 ? color.yellow : na
barcolor(c) 
